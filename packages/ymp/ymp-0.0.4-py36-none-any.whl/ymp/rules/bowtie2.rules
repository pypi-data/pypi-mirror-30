Env(name="bowtie2", base="bioconda", packages=["bowtie2", "samtools"])

BT2IDX_SUFFIXES = "1.bt2 2.bt2 3.bt2 4.bt2 rev.1.bt2 rev.2.bt2".split()

rule bowtie2_index:
    message:
        "Bowtie2: Indexing {input.contigs}"
    input:
        contigs="{path}/{source}.contigs.fasta.gz"
    output:
        temp(expand("{{path}}/{{source}}.contigs.{ext}", ext=BT2IDX_SUFFIXES))
    params:
        bt2_base="{path}/{source}.contigs"
    threads:
        8
    log:
        "{path}/{source}.contigs.btbuild.log"
    conda:
        "bowtie2"
    shell:"""
    bowtie2-build-s \
      {input.contigs} \
      {params.bt2_base} \
      --threads {threads} \
      >& {log}
    """

with Stage("map_bowtie2") as S:
    S.doc("""
    Map reads using Bowtie2
    """)
    rule bowtie2_map:
        message:
            "Bowtie2: Mapping {input.fq[0]} to {input.fa}"
        input:
            fq    =        "{:prev:}/{source}.{: pairnames :}.fq.gz",
            fa    =        "{:reference:}.fasta.gz",
            index = expand("{{:reference:}}.{ext}", ext=BT2IDX_SUFFIXES)
        output:
            bam   = temp(  "{:this:}/{target}.{source}.bam"),
            stats =        "{:this:}/{target}.{source}.stats"
        log:
                           "{:this:}/{target}.{source}.log"
        params:
            bt2_base =     "{:reference:}",
            maxins = 800,
            k = 100,  # max number of secondary alignments
            mem = icfg.mem("8g")
        threads:
            16
        conda:
            "bowtie2"
        shell:"""
        bowtie2 \
            -x {params.bt2_base} \
            -1 {input.fq[0]} -2 {input.fq[1]} \
            -X {params.maxins} \
            --met-file {output.stats} \
            -k {params.k} \
            -p {threads} \
            2>{log} \
            | samtools view -b -o {output.bam} -
        """

