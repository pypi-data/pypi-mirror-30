Env(name="fastqc", base="bioconda", packages=[
    "fastqc",
    "conda-forge::openjdk",  # avoid default channel, it's missing fonts
    "freetype = 2.7"         # and conda-forge jdk is missing this dependency
])


with Stage("qc_fastqc") as S:
    S.doc("""
    Quality screen reads using FastQC
    """)

    rule qc_fastqc:
        """Run FastQC on read files"""
        message:
            "Creating QC report for {input}"
        input:
            "{:prev:}/{file}.fq.gz"
        output:
            "{:this:}/{file}_fastqc.html",
            "{:this:}/{file}_fastqc.zip"
        log:
            "{:this:}/{file}_fastqc.log"
        threads:
            1
        params:
            k=10,
            mem=icfg.mem("8g")
        conda:
            "fastqc"
        shell: """
        # override too low memory requested by fastqc 0.11.5 of 256M per thread:
        export _JAVA_OPTIONS="-Xmx{params.mem}m"

        fastqc \
         -t {threads} \
         -o $(dirname {output[0]}) \
         {input} \
         -k {params.k} \
         >{log} 2>&1
        """

    rule all_fastqc:
        message:
            "Finished {output}"
        input:
            "{:this:}/{:fq_names:}_fastqc.zip"
        output:
            touch("{:this:}/all")
